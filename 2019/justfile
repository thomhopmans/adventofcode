
set dotenv-load

# display help information
default:
  @just --list

# run exercise of DAY
run EXERCISE:
    uv run python adventofcode/run.py {{EXERCISE}}

# run all exercises
run-all:
    uv run python adventofcode/run.py all

# test exercise of DAY
test EXERCISE:
    uv run python -m pytest tests/exercises/test_exercise_{{EXERCISE}}.py

# test all exercises
test-all:
    uv run python -m pytest tests/

# download inputs of DAY
download DAY:
    #!/usr/bin/env bash
    YEAR=2019
    PUZZLE_URL="https://adventofcode.com/$YEAR/day/{{DAY}}/input"
    PUZZLE_FILE="adventofcode/inputs/exercise{{DAY}}.txt"
    curl "${PUZZLE_URL}" -H "cookie: session=${AOC_SESSION_COOKIE}" -o "${PUZZLE_FILE}" 2>/dev/null