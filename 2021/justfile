
set dotenv-load

# display help information
default:
  @just --list

# run exercise of DAY
run EXERCISE:
    poetry run python adventofcode/run.py {{EXERCISE}}

# run all exercises
run-all:
    poetry run python adventofcode/run.py

# test exercise of DAY
test EXERCISE:
    poetry run python -m pytest adventofcode/tests/test_exercise_{{EXERCISE}}.py

# test all exercises
test-all:
    poetry run python -m pytest tests/

# download inputs of DAY
download DAY:
    #!/usr/bin/env bash
    YEAR=2021
    PUZZLE_URL="https://adventofcode.com/$YEAR/day/{{DAY}}/input"
    PUZZLE_FILE="adventofcode/inputs/exercise{{DAY}}.txt"
    curl "${PUZZLE_URL}" -H "cookie: session=${AOC_SESSION_COOKIE}" -o "${PUZZLE_FILE}" 2>/dev/null